/**
 * File:	include/ldap-server/wizards.ycp
 * Package:	Configuration of ldap-server
 * Summary:	Wizards definitions
 * Authors:	Andreas Bauer <abauer@suse.de>
 *
 * $Id$
 */

{

textdomain "ldap-server";

import "Sequencer";
import "Wizard";

include "ldap-server/complex.ycp";
include "ldap-server/dialogs.ycp";

/**
 * Main workflow of the ldap-server configuration
 * @return sequence result
 */
any MainSequence() {

    /* FIXME: adapt to your needs */
    map aliases = $[
    "enable"       : ``( EnableServiceDialog() ),
    "tree"         : ``( TreeDialog() )
    ];

    map sequence = $[
	    "ws_start"  : "enable",
        "enable"    : $[
            `abort      : `abort,
            `back       : `abort,
            `cancel     : `abort,
            `next       : `next,
            `config     : "tree"
        ],
        "tree"      : $[
            `abort      : `abort,
            `back       : `abort,
            `cancel     : `abort,
            `next       : `next
        ]
    ];

    any ret = Sequencer::Run(aliases, sequence);
    return ret;
}

any ProposalSequence() {
    map aliases = $[
        "propose"   : ``( ProposalDialog() )
    ];

    map sequence = $[
        "ws_start"  : "propose",
        "propose"   : $[
            `next   : `next,
            `back   : `back,
            `abort  : `back,
            `cancel : `back
        ]
    ];

    y2milestone( "--> starting ProposalSequence" );

    any ret = Sequencer::Run(aliases, sequence);

    y2milestone( "--> ProposalSequence finished " );

    return ret;
}

/**
 * Whole configuration of ldap-server
 * @return sequence result
 */
any LdapServerSequence() {

    map aliases = $[
	"read"	: [ ``( ReadDialog() ), true ],
	"main"	:   ``( MainSequence() ),
	"write"	: [ ``( WriteDialog() ), true ]
    ];

    map sequence = $[
	"ws_start" : "read",
	"read" : $[
	    `abort	: `abort,
	    `next	: "main"
	],
	"main" : $[
	    `abort	: `abort,
	    `next	: "write"
	],
	"write" : $[
	    `abort	: `abort,
	    `next	: `next
	]
    ];

    Wizard::CreateDialog();

    any ret = Sequencer::Run(aliases, sequence);

    UI::CloseDialog();
    return ret;
}

/**
 * Whole configuration of ldap-server but without reading and writing.
 * For use with autoinstallation.
 * @return sequence result
 */
any LdapServerAutoSequence() {

    /* Initialization dialog caption */
    string caption = _("LDAP Server Configuration");
    /* Initialization dialog contents */
    term contents = `Label(_("Initializing..."));

    y2milestone ( "--> starting LdapServerAutoSequence" );

    Wizard::CreateDialog();
    Wizard::SetContentsButtons(caption, contents, "",
	    Label::BackButton(), Label::NextButton());

    any ret = ProposalSequence();

    UI::CloseDialog();

    y2milestone( "--> LdapServerAutoSequence finished " );

    return ret;
}

/* EOF */
}
