/**
 * File:	include/ldap-server/wizards.ycp
 * Package:	Configuration of ldap-server
 * Summary:	Wizards definitions
 * Authors:	Andreas Bauer <abauer@suse.de>
 *
 * $Id$
 */

{

textdomain "ldap-server";

import "Sequencer";
import "Wizard";

include "ldap-server/complex.ycp";
include "ldap-server/dialogs.ycp";

/**
 * Main workflow of the ldap-server configuration
 * @return sequence result
 */
any MainSequence() {

    /* FIXME: adapt to your needs */
    map aliases = $[
    "tree"         : ``( TreeDialog() )
    ];

    map sequence = $[
	    "ws_start"  : "tree",
        "tree"      : $[
            `abort      : `abort,
            `back       : `abort,
            `cancel     : `abort,
            `next       : `next
        ]
    ];

    any ret = Sequencer::Run(aliases, sequence);
    UI::CloseDialog();
    return ret;
}

any ProposalSequence() {

    /* Initialization dialog caption */
    string caption = _("LDAP Server Configuration");
    /* Initialization dialog contents */
    term contents = `Label(_("Initializing..."));

    Wizard::CreateDialog();
    Wizard::SetContentsButtons(caption, contents, "",
           Label::BackButton(), Label::NextButton());

    map aliases = $[
        "startup"   : ``( EnableServiceDialog() ),
        "database"   : ``( ProposalDialog() ),
        "summary"   : ``(SummaryDialog() ),
        "advanced"  :  ``(MainSequence() ),
	"write"	: ``( WriteDialog() )
    ];

    map sequence = $[
        "ws_start"  : "startup",
        "startup"   : $[
            `next   : "database"
        ],
        "database"   : $[
            `next   : "summary",
            `back   : `back,
            `abort  : `back,
            `cancel : `back
        ],
        "summary"   : $[
            `next   : "write",
            `back   : `back,
            `abort  : `back,
            `cancel : `back,
            `advanced : "advanced"
        ],
	"advanced" : $[
	    `abort	: `abort,
	    `next	: "write"
	],
	"write" : $[
	    `abort	: `abort,
	    `next	: `next
	]
    ];

    y2milestone( "--> starting ProposalSequence" );

    any ret = Sequencer::Run(aliases, sequence);

    y2milestone( "--> ProposalSequence finished " );

    UI::CloseDialog();

    return ret;
}

/**
 * Whole configuration of ldap-server
 * @return sequence result
 */
any LdapServerSequence() {

    map aliases = $[
	"read"	: ``( ReadDialog() ),
        "propose" : ``( ProposalSequence() ),
	"main"	: ``( MainSequence() ),
	"write"	: ``( WriteDialog() )
    ];

    map sequence = $[
	"ws_start" : "read",
	"read" : $[
	    `abort	: `abort,
            `initial    : "propose",
	    `next	: "main"
	],
        "propose" : $[
            `next       : `next
        ],
	"main" : $[
	    `abort	: `abort,
	    `next	: "write"
	],
	"write" : $[
	    `abort	: `abort,
	    `next	: `next
	]
    ];

    Wizard::CreateDialog();

    any ret = Sequencer::Run(aliases, sequence);

    UI::CloseDialog();
    return ret;
}

/**
 * Whole configuration of ldap-server but without reading and writing.
 * For use with autoinstallation.
 * @return sequence result
 */
any LdapServerAutoSequence() {

    /* Initialization dialog caption */
    string caption = _("LDAP Server Configuration");
    /* Initialization dialog contents */
    term contents = `Label(_("Initializing..."));

    y2milestone ( "--> starting LdapServerAutoSequence" );

    Wizard::CreateDialog();
    Wizard::SetContentsButtons(caption, contents, "",
	    Label::BackButton(), Label::NextButton());

    any ret = MainSequence();

    UI::CloseDialog();

    y2milestone( "--> LdapServerAutoSequence finished " );

    return ret;
}

/* EOF */
}
